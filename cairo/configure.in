AC_PREREQ(2.58)

dnl cairo package version number, (as distinct from shared library version)
dnl For the minor number: odd => unstable series
dnl			  even => stable series
dnl For the micro number: odd => in-progress development (from git)
dnl			  even => tar-file snapshot or release
m4_define(cairo_version_major, 1)
m4_define(cairo_version_minor, 6)
m4_define(cairo_version_micro, 4)

AC_INIT([cairo],
      cairo_version_major.cairo_version_minor.cairo_version_micro,
      [http://bugs.freedesktop.org/enter_bug.cgi?product=cairo])
AC_CONFIG_SRCDIR(src/cairo.h)
AC_CONFIG_HEADERS(config.h)

dnl automake 1.8 requires autoconf 2.58
dnl automake 1.7 requires autoconf 2.54
dnl automake < 1.8 does not handle TESTS=some-test$(EXEXT) as used
dnl in test/Makefile.am and perf/Makefile.am
AM_INIT_AUTOMAKE([1.8])

dnl ===========================================================================

# libtool shared library version

# Increment if the interface has additions, changes, removals.
m4_define(LT_CURRENT, 19)

# Increment any time the source changes; set to
# 0 if you increment CURRENT
m4_define(LT_REVISION, 5)

# Increment if any interfaces have been added; set to 0
# if any interfaces have been removed. removal has
# precedence over adding, so set to 0 if both happened.
+m4_define(LT_AGE, 17)

dnl ===========================================================================

VERSION_INFO="LT_CURRENT():LT_REVISION():LT_AGE()"
AC_SUBST(VERSION_INFO)

LT_CURRENT_MINUS_AGE=m4_eval(LT_CURRENT() - LT_AGE())
AC_SUBST(LT_CURRENT_MINUS_AGE)

CAIRO_VERSION_MAJOR=cairo_version_major()
CAIRO_VERSION_MINOR=cairo_version_minor()
CAIRO_VERSION_MICRO=cairo_version_micro()
AC_SUBST(CAIRO_VERSION_MAJOR)
AC_SUBST(CAIRO_VERSION_MINOR)
AC_SUBST(CAIRO_VERSION_MICRO)

dnl ===========================================================================

AC_PATH_PROG(FIND, find)
AC_PATH_PROG(XARGS, xargs)

dnl ===========================================================================

AC_PROG_CC
AC_PROG_CPP
AC_LIBTOOL_WIN32_DLL
AC_PROG_LIBTOOL dnl required version (1.4) DON'T REMOVE!
AC_STDC_HEADERS
AC_C_INLINE

case $host_os in
	darwin*)
AH_VERBATIM([X_BYTE_ORDER],[
/* Deal with multiple architecture compiles on Mac OS X */
#ifdef __APPLE_CC__
#ifdef __BIG_ENDIAN__
#define WORDS_BIGENDIAN 1
#define FLOAT_WORDS_BIGENDIAN 1
#else
#undef WORDS_BIGENDIAN
#undef FLOAT_WORDS_BIGENDIAN
#endif
#endif
])
	;;
	*) 
AC_C_BIGENDIAN
AX_C_FLOAT_WORDS_BIGENDIAN
	;;
esac

dnl ===========================================================================
dnl === Local macros
dnl ===========================================================================

dnl _CHECK_FUNCS_WITH_FLAGS(FUNCTION..., CFLAGS, LIBS
dnl                         [, ACTION-IF-FOUND [, ACTION-IF-NOT-FOUND]])
dnl Like AC_CHECK_FUNCS but with additional CFLAGS and LIBS
dnl --------------------------------------------------------------------
AC_DEFUN([_CHECK_FUNCS_WITH_FLAGS],
[ 
  save_cflags="$CFLAGS"
  save_libs="$LIBS"
  CFLAGS="$CFLAGS $2"
  LIBS="$LIBS $3"
  AC_CHECK_FUNCS($1, $4, $5)
  CFLAGS="$save_cflags"
  LIBS="$save_libs"
])

dnl ===========================================================================

AC_CHECK_FUNCS(vasnprintf ctime_r drand48)

AC_MSG_CHECKING([for Sun Solaris (non-POSIX ctime_r)])
case "$host" in
  *-*-solaris*)
    CFLAGS="$CFLAGS -D_POSIX_PTHREAD_SEMANTICS"
    solaris_posix_pthread=yes
    ;;
  *)
    solaris_posix_pthread=no
    ;;
esac
AC_MSG_RESULT([$solaris_posix_pthread])

dnl ===========================================================================
dnl
dnl Test for native atomic operations.
dnl
AC_MSG_CHECKING([for native atomic primitives])
cairo_atomic_primitives="none"

AC_TRY_LINK([int atomic_add(int i) { return __sync_fetch_and_add (&i, 1); }], [],
  AC_DEFINE(CAIRO_HAS_INTEL_ATOMIC_PRIMITIVES, 1, [Enable if your compiler supports the Intel __sync_* atomic primitives])
  cairo_atomic_primitives="Intel"
  )

AC_MSG_RESULT([$cairo_atomic_primitives])

AC_MSG_CHECKING([whether atomic ops require a memory barrier])
case $host_cpu in
    i?86)
	cairo_atomic_op_needs_memory_barrier="no"
	;;
    x86_64)
	cairo_atomic_op_needs_memory_barrier="no"
	;;
    arm*)
	cairo_atomic_op_needs_memory_barrier="no"
	;;
    *)
	cairo_atomic_op_needs_memory_barrier="yes"
	;;
esac
if test "x$cairo_atomic_op_needs_memory_barrier" = "xyes"; then
    AC_DEFINE_UNQUOTED(CAIRO_ATOMIC_OP_NEEDS_MEMORY_BARRIER, 1,
		       [whether Cairo needs memory barriers around atomic ops])
fi
AC_MSG_RESULT([$cairo_atomic_op_needs_memory_barrier])

dnl ===========================================================================

AC_CHECK_LIBM
LIBS="$LIBS $LIBM"

AC_CHECK_LIB(z, compress,
	 [AC_CHECK_HEADER(zlib.h, [have_libz=yes],
	 [have_libz="no (requires zlib http://www.gzip.org/zlib/)"])],
	 [have_libz="no (requires zlib http://www.gzip.org/zlib/)"])

dnl ===========================================================================

PKG_PROG_PKG_CONFIG()
if test "x$PKG_CONFIG" = x; then
	AC_MSG_ERROR([pkg-config >= $PKGCONFIG_REQUIRED required but not found (http://pkgconfig.freedesktop.org/)])
fi


dnl ===========================================================================
dnl
dnl cairo_cache_version should be increased every time that the backend
dnl detection stuff changes in a way that removing the config.cache file may be
dnl needed for correct operation. (this is only for the backend detection
dnl changes;  it doesn't have any effect on any other cached thing.)
dnl
m4_define(cairo_cache_version, 4)

dnl ===========================================================================
dnl
dnl Define a macro to enable backends.
dnl  - Macro: CAIRO_BACKEND_ENABLE (NAMESPACE, NAME, ARG, FEATURE_NAME, DEFAULT, COMMANDS)
dnl
dnl where:
dnl
dnl	NAMESPACE is the sub-namespace in function names, eg. "ft" for cairo_ft_...
dnl	NAME is the human-readable name of the backend, eg. "FreeType font"
dnl	ARG is what enables the backend, eg. "freetype" for --enable-freetype
dnl	FEATURE_NAME is what's used in cairo-features.h, eg. FT_FONT for CAIRO_HAS_FT_FONT
dnl	DEFAULT is the default state of the backend:
dnl		"no" for experimental backends, eg. your favorite new backend
dnl		"yes" for mandatory backends, eg. png
dnl		"auto" for other supported backends, eg. xlib
dnl	COMMANDS are run to check whether the backend can be enabled.  Their
dnl		result may be cached, so user should not count on them being run.
dnl		They should set use_$(NAMESPACE) to something other than yes if the
dnl		backend cannot be built, eg. "no (requires SomeThing)".  It then
dnl		should also set $(NAMESPACE)_REQUIRES/CFLAGS/LIBS/...
dnl		appropriately.  Look at the macro definition for more details,
dnl		or ask if in doubt.
dnl
AC_DEFUN([CAIRO_BACKEND_ENABLE],
         [AC_ARG_ENABLE([$3],
                         AS_HELP_STRING([--enable-$3=@<:@no/auto/yes@:>@],
                                        [Enable cairo's $2 backend @<:@default=$5@:>@]),
                         enable_$1=$enableval, enable_$1=$5)
	  if test "x$enable_$1" = xno; then
	    use_$1="no (disabled, use --enable-$3 to enable)"
	  else
	    if test "x$cairo_cv_backend_[]$1[]_cache_version" != "x[]cairo_cache_version"; then
	      # cached results for this backend (if any) are stale.  force rechecking.
	      unset cairo_cv_backend_[]$1[]_use
	    fi
            AC_CACHE_CHECK([for cairo's $2 backend], cairo_cv_backend_[]$1[]_use,
	    		   [echo
			    use_[]$1=yes
			    $1[]_REQUIRES=$ac_env_[]$1[]_REQUIRES_value
			    $1[]_CFLAGS=$ac_env_[]$1[]_CFLAGS_value
			    $1[]_LIBS=$ac_env_[]$1[]_LIBS_value
			    $1[]_NONPKGCONFIG_CFLAGS=$ac_env_[]$1[]_NONPKGCONFIG_CFLAGS_value
			    $1[]_NONPKGCONFIG_LIBS=$ac_env_[]$1[]_NONPKGCONFIG_LIBS_value
			    $1[]_BASE=cairo
			    $6
			    cairo_cv_backend_[]$1[]_use=$use_[]$1
			    cairo_cv_backend_[]$1[]_cache_version=cairo_cache_version
			    cairo_cv_backend_[]$1[]_requires=$[]$1[]_REQUIRES
			    cairo_cv_backend_[]$1[]_cflags=$[]$1[]_CFLAGS
			    cairo_cv_backend_[]$1[]_libs=$[]$1[]_LIBS
			    cairo_cv_backend_[]$1[]_nonpkgconfig_cflags=$[]$1[]_NONPKGCONFIG_CFLAGS
			    cairo_cv_backend_[]$1[]_nonpkgconfig_libs=$[]$1[]_NONPKGCONFIG_LIBS
			    cairo_cv_backend_[]$1[]_base=$[]$1[]_BASE
			    AC_MSG_CHECKING([whether cairo's $2 backend could be enabled])])

	    use_[]$1=$cairo_cv_backend_[]$1[]_use
	    $1[]_BASE=$cairo_cv_backend_[]$1[]_base

	    $1[]_REQUIRES="$cairo_cv_backend_[]$1[]_requires "
	    $1[]_CFLAGS="$cairo_cv_backend_[]$1[]_cflags "
	    $1[]_LIBS="$cairo_cv_backend_[]$1[]_libs "
	    $1[]_NONPKGCONFIG_CFLAGS="$cairo_cv_backend_[]$1[]_nonpkgconfig_cflags "
	    $1[]_NONPKGCONFIG_LIBS="$cairo_cv_backend_[]$1[]_nonpkgconfig_libs "

	    # null the ones that only have space
	    test "x$$1[]_REQUIRES" = "x " && $1[]_REQUIRES=""
	    test "x$$1[]_CFLAGS" = "x " && $1[]_CFLAGS=""
	    test "x$$1[]_LIBS" = "x " && $1[]_LIBS=""
	    test "x$$1[]_NONPKGCONFIG_CFLAGS" = "x " && $1[]_NONPKGCONFIG_CFLAGS=""
	    test "x$$1[]_NONPKGCONFIG_LIBS" = "x " && $1[]_NONPKGCONFIG_LIBS=""

	    case $enable_[]$1 in
	      yes)
	        AS_IF([test "x$use_[]$1" = xyes],,[
	          AC_MSG_ERROR([requested $2 backend could not be enabled])
		])
		;;
	      auto)
		;;
	      *)
	        AC_MSG_ERROR([invalid argument passed to --enable-$3: $use_$1, should be one of @<:@no/auto/yes@:>@])
		;;
	    esac
	    if test "x$use_[]$1" = xyes; then
	      CAIRO_FEATURES="$4 $CAIRO_FEATURES"
	      CAIRO_REQUIRES="$$1_REQUIRES$CAIRO_REQUIRES"
	      CAIRO_CFLAGS="$$1_NONPKGCONFIG_CFLAGS$$1_CFLAGS$CAIRO_CFLAGS"
	      CAIRO_LIBS="$$1_NONPKGCONFIG_LIBS$$1_LIBS$CAIRO_LIBS"
	      CAIRO_NONPKGCONFIG_CFLAGS="$$1_NONPKGCONFIG_CFLAGS$CAIRO_NONPKGCONFIG_CFLAGS"
	      CAIRO_NONPKGCONFIG_LIBS="$$1_NONPKGCONFIG_LIBS$CAIRO_NONPKGCONFIG_LIBS"
	      outfile=`echo src/cairo-$1.pc | sed -e s/_/-/g`
	      AC_MSG_NOTICE([creating $outfile])
	      mkdir -p src
	      AS_IF([sed \
			-e "s,@backend_name@,$1,g" \
			-e "s,@Backend_Name@,$2,g" \
			-e "s,@BACKEND_BASE@,$$1_BASE,g" \
			-e "s,@BACKEND_REQUIRES@,$$1_REQUIRES,g" \
			-e "s%@BACKEND_NONPKGCONFIG_LIBS@%$$1_NONPKGCONFIG_LIBS%g" \
			-e "s,@BACKEND_NONPKGCONFIG_CFLAGS@,$$1_NONPKGCONFIG_CFLAGS,g" \
			-e "s,@prefix@,$prefix,g" \
			-e "s,@exec_prefix@,$exec_prefix,g" \
			-e "s,@libdir@,$libdir,g" \
			-e "s,@includedir@,$includedir,g" \
			-e "s,@VERSION@,$VERSION,g" \
	               $srcdir/src/cairo-backend.pc.in > $outfile],,[
		rm -f "$outfile"
		AC_MSG_ERROR([failed creating $outfile])
	      ])
	    else
	      CAIRO_NO_FEATURES="$4 $CAIRO_NO_FEATURES"
	    fi
	  fi
	  AM_CONDITIONAL(CAIRO_HAS_$4, test "x$use_$1" = xyes)
	])

CAIRO_FEATURES=""
CAIRO_NO_FEATURES=""
CAIRO_REQUIRES=""
CAIRO_NONPKGCONFIG_CFLAGS=""
CAIRO_NONPKGCONFIG_LIBS="$LIBM"
CAIRO_LDADD=""
CAIRO_CFLAGS=$CAIRO_NONPKGCONFIG_CFLAGS
CAIRO_LIBS=$CAIRO_NONPKGCONFIG_LIBS

dnl ===========================================================================

#PIXMAN_VERSION="0.10.0"
#PIXMAN_REQUIRES="pixman-1 >= $PIXMAN_VERSION"
#PKG_CHECK_MODULES(pixman, $PIXMAN_REQUIRES, ,
#	[AC_MSG_ERROR([pixman >= $PIXMAN_VERSION is required
#(http://cairographics.org/releases/)])])

pixman_CFLAGS='-I$(top_srcdir)/../pixman/pixman'
pixman_LIBS='$(top_builddir)/../pixman/pixman/libpixman-1.la'

#CAIRO_REQUIRES="$PIXMAN_REQUIRES $CAIRO_REQUIRES"
CAIRO_CFLAGS="$pixman_CFLAGS $CAIRO_CFLAGS"
CAIRO_LIBS="$pixman_LIBS $CAIRO_LIBS"

dnl ===========================================================================

CAIRO_BACKEND_ENABLE(xlib, Xlib, xlib, XLIB_SURFACE, auto, [
  xlib_REQUIRES="x11"
  PKG_CHECK_MODULES(xlib, $xlib_REQUIRES, ,
		    [AC_MSG_RESULT(no)
		     xlib_REQUIRES=""
		     AC_PATH_XTRA
		     if test "x$no_x" = xyes; then
		       use_xlib="no (requires X development libraries)"
		     else
		       xlib_NONPKGCONFIG_LIBS="$X_PRE_LIBS $X_LIBS -lX11 $X_EXTRA_LIBS"
		       xlib_NONPKGCONFIG_CFLAGS=$X_CFLAGS
		     fi])
])

CAIRO_BACKEND_ENABLE(xlib_xrender, Xlib Xrender, xlib-xrender, XLIB_XRENDER_SURFACE, auto, [
  if test "x$use_xlib" != "xyes"; then
    use_xlib_xrender="no (requires --enable-xlib)"
  else
    dnl Check for Xrender header files if the Xrender package is not installed:
    xlib_xrender_BASE=cairo-xlib
    xlib_xrender_REQUIRES="xrender >= 0.6"
    PKG_CHECK_MODULES(xlib_xrender, $xlib_xrender_REQUIRES, ,
		      [AC_MSG_RESULT(no)
		       xlib_xrender_REQUIRES=""
		       old_CPPFLAGS=$CPPFLAGS
		       CPPFLAGS="$CPPFLAGS $xlib_CFLAGS $xlib_NONPKGCONFIG_CFLAGS"
		       AC_CHECK_HEADER(X11/extensions/Xrender.h,
				       [xlib_xrender_NONPKGCONFIG_LIBS="-lXrender"],
				       [use_xlib_xrender="no (requires Xrender http://freedesktop.org/Software/xlibs)"])
		       CPPFLAGS=$old_CPPFLAGS
		      ])
  fi
])

dnl ===========================================================================

CAIRO_BACKEND_ENABLE(xcb, XCB, xcb, XCB_SURFACE, no, [
  xcb_REQUIRES="xcb >= 0.9.92 xcb-render >= 0.9.92 xcb-renderutil"
  PKG_CHECK_MODULES(xcb, $xcb_REQUIRES, , [AC_MSG_RESULT(no)
  use_xcb="no (requires XCB http://xcb.freedesktop.org)"])
])

dnl ===========================================================================

CAIRO_BACKEND_ENABLE(quartz, Quartz, quartz, QUARTZ_SURFACE, auto, [
  dnl There is no pkgconfig for quartz; lets do a header check
  AC_CHECK_HEADER(ApplicationServices/ApplicationServices.h, , [use_quartz="no (ApplicationServices framework not found)"])
  if test "x$use_quartz" != "xyes" ; then
    dnl check for CoreGraphics as a separate framework
    AC_CHECK_HEADER(CoreGraphics/CoreGraphics.h, , [use_quartz="no (CoreGraphics framework not found)"])
    quartz_LIBS="-Xlinker -framework -Xlinker CoreGraphics"
  else
    quartz_LIBS="-Xlinker -framework -Xlinker ApplicationServices"
  fi
])

CAIRO_BACKEND_ENABLE(quartz_font, Quartz font, quartz-font, QUARTZ_FONT, auto, [
  use_quartz_font=$use_quartz
])

CAIRO_BACKEND_ENABLE(quartz_image, Quartz Image, quartz-image, QUARTZ_IMAGE_SURFACE, no, [
  use_quartz_image=$use_quartz
])

dnl ===========================================================================

AC_MSG_CHECKING([for native Win32])
case "$host" in
  *-*-mingw*)
    cairo_os_win32=yes
    ;;
  *)
    cairo_os_win32=no
    ;;
esac
AC_MSG_RESULT([$cairo_os_win32])
AM_CONDITIONAL(OS_WIN32, test "$cairo_os_win32" = "yes")

AC_CHECK_HEADERS([windows.h], have_windows=yes, have_windows=no)

CAIRO_BACKEND_ENABLE(win32, Microsoft Windows, win32, WIN32_SURFACE, auto, [
  if test "x$have_windows" != xyes; then
    use_win32="no (requires a Win32 platform)"
  fi
  win32_LIBS="-lgdi32 -lmsimg32"
])

CAIRO_BACKEND_ENABLE(win32_font, Microsoft Windows font, win32-font, WIN32_FONT, auto, [
  use_win32_font=$use_win32
])

test_win32_printing=no
if test "x$use_win32" = "xyes"; then
  AC_CHECK_PROG(GS, gs, gs)
  if test "$GS"; then
    AC_DEFINE([CAIRO_CAN_TEST_WIN32_PRINTING_SURFACE], 1, [Define to 1 if the Win32 Printing backend can be tested (needs ghostscript)])
    test_win32_printing="yes"
  else
    AC_MSG_WARN([Win32 Printing backend will not be tested since ghostscript is not available])
    test_win32_printing="no (requires ghostscript)"
  fi
fi

AM_CONDITIONAL(CAIRO_CAN_TEST_WIN32_PRINTING_SURFACE, test "x$test_win32_printing" = "xyes")

dnl ===========================================================================

CAIRO_BACKEND_ENABLE(os2, OS/2, os2, OS2_SURFACE, no, [
  case "$host" in
    *-*-os2*)
      :
      ;;
    *)
      use_os2="no (requires an OS/2 platform)"
      ;;
  esac
])

dnl ===========================================================================

CAIRO_BACKEND_ENABLE(beos, BeOS/Zeta, beos, BEOS_SURFACE, no, [
  case "$host" in
    *-*-beos)
      dnl Add libbe and libzeta if available
      AC_CHECK_LIB(be,main,beos_LIBS="$beos_LIBS -lbe")
      AC_CHECK_LIB(zeta,main,beos_LIBS="$beos_LIBS -lzeta")
      ;;
    *)
      use_beos="no (requires a BeOS platform)"
      ;;
  esac
])

# Uncomment the if, since with it in place compilation breaks on OpenSuSE 11.1b4
#if test "x$use_beos" = "xyes"; then
  AC_PROG_CXX
#fi

dnl ===========================================================================

CAIRO_BACKEND_ENABLE(png, PNG, png, PNG_FUNCTIONS, yes, [
  use_png=no
  AC_ARG_VAR([png_REQUIRES], [module name for libpng to search for using pkg-config])
  if test "x$png_REQUIRES" = x; then
    # libpng13 is GnuWin32's libpng-1.2.8 :-(
    for l in libpng12 libpng13 libpng10 ; do
      if $PKG_CONFIG --exists $l ; then
        png_REQUIRES=$l
        use_png=yes
        break
      fi
    done
  else
    use_png=yes
  fi

  if test "x$use_png" = "xyes" ; then 
    PKG_CHECK_MODULES(png, $png_REQUIRES, , AC_MSG_RESULT(no))
  else
    AC_MSG_WARN([Could not find libpng in the pkg-config search path])
  fi    
])

if test "x$use_png" = "xyes"; then
  :
else
  AC_MSG_WARN([*** To run the tests, cairo must be built with png support ***])
fi

dnl ===========================================================================

GLITZ_MIN_VERSION=0.5.1
CAIRO_BACKEND_ENABLE(glitz, glitz, glitz, GLITZ_SURFACE, no, [
  glitz_REQUIRES="glitz >= $GLITZ_MIN_VERSION"
  PKG_CHECK_MODULES(glitz, $glitz_REQUIRES, , [AC_MSG_RESULT(no)
  use_glitz="no (requires glitz http://freedesktop.org/Software/glitz)"])
])

if test "x$use_glitz" = "xyes";then
  PKG_CHECK_MODULES(GLITZ_AGL, glitz-agl >= $GLITZ_MIN_VERSION,
   [have_glitz_agl=yes], [have_glitz_agl=no; AC_MSG_RESULT(no)])
  PKG_CHECK_MODULES(GLITZ_EGL, glitz-egl >= $GLITZ_MIN_VERSION,
   [have_glitz_egl=yes], [have_glitz_egl=no; AC_MSG_RESULT(no)])
  PKG_CHECK_MODULES(GLITZ_GLX, glitz-glx >= $GLITZ_MIN_VERSION,
   [have_glitz_glx=yes], [have_glitz_glx=no; AC_MSG_RESULT(no)])
  PKG_CHECK_MODULES(GLITZ_WGL, glitz-wgl >= $GLITZ_MIN_VERSION,
   [have_glitz_wgl=yes], [have_glitz_wgl=no; AC_MSG_RESULT(no)])

  if test "x$have_glitz_agl" = "xyes";then
    AC_DEFINE(CAIRO_CAN_TEST_GLITZ_AGL_SURFACE, 1, [define if glitz backend can be tested against agl])
  fi
  if test "x$have_glitz_egl" = "xyes";then
    AC_DEFINE(CAIRO_CAN_TEST_GLITZ_EGL_SURFACE, 1, [define if glitz backend can be tested against egl])
  fi
  if test "x$have_glitz_glx" = "xyes";then
    AC_DEFINE(CAIRO_CAN_TEST_GLITZ_GLX_SURFACE, 1, [define if glitz backend can be tested against glx])
  fi
  if test "x$have_glitz_wgl" = "xyes";then
    AC_DEFINE(CAIRO_CAN_TEST_GLITZ_WGL_SURFACE, 1, [define if glitz backend can be tested against wgl])
  fi
fi

AM_CONDITIONAL(CAIRO_CAN_TEST_GLITZ_AGL_SURFACE, test "x$have_glitz_agl" = "xyes")
AM_CONDITIONAL(CAIRO_CAN_TEST_GLITZ_EGL_SURFACE, test "x$have_glitz_egl" = "xyes")
AM_CONDITIONAL(CAIRO_CAN_TEST_GLITZ_GLX_SURFACE, test "x$have_glitz_glx" = "xyes")
AM_CONDITIONAL(CAIRO_CAN_TEST_GLITZ_WGL_SURFACE, test "x$have_glitz_wgl" = "xyes")

dnl ===========================================================================

CAIRO_BACKEND_ENABLE(directfb, directfb, directfb, DIRECTFB_SURFACE, no, [
  directfb_REQUIRES=directfb
  PKG_CHECK_MODULES(directfb, $directfb_REQUIRES, , AC_MSG_RESULT(no)
		    [use_directfb="no (requires directfb http://www.directfb.org)"])
])

dnl ===========================================================================

# We use pkg-config to look for freetype2, but fall back to
# freetype-config if it fails.  We prefer pkg-config, since we can
# then just put freetype2 >= $FREETYPE_MIN_VERSION in
# Requires.private, but at least up to 2003-06-07, there was no
# freetype2.pc in the release.
#
# FreeType versions come in three forms:
#   release (such as 2.1.9)
#   libtool (such as 9.7.3) (returned by freetype-config and pkg-config)
#   platform-specific/soname (such as 6.3.4)
# and they recommend you never use the platform-specific version
# (see docs/VERSION.DLL in freetype2 sources)
#
# Set these as appropriate:

# release number - for information only
FREETYPE_MIN_RELEASE=2.1.0
# libtool-specific version - this is what is checked
FREETYPE_MIN_VERSION=8.0.2

CAIRO_BACKEND_ENABLE(ft, FreeType font, freetype, FT_FONT, auto, [
  ft_REQUIRES="fontconfig"
  PKG_CHECK_MODULES(FONTCONFIG, $ft_REQUIRES,
		    [use_ft=yes],
		    [AC_MSG_RESULT(no)
		     use_ft=no])

  if test "x$use_ft" = "xyes"; then
    PKG_CHECK_MODULES(FREETYPE, freetype2 >= $FREETYPE_MIN_VERSION,
                      [freetype_pkgconfig=yes],
		      [AC_MSG_RESULT(no)
		       freetype_pkgconfig=no])
  
    if test "x$freetype_pkgconfig" = "xyes"; then
      ft_REQUIRES="freetype2 >= $FREETYPE_MIN_VERSION $ft_REQUIRES"
    else
  
      if test -z "$FREETYPE_CONFIG"; then
        AC_PATH_PROG(FREETYPE_CONFIG, freetype-config, no)
      fi
      if test "x$FREETYPE_CONFIG" = "xno" ; then
        use_ft='no (freetype-config not found in path or $FREETYPE_CONFIG)'
      else
        AC_MSG_CHECKING(freetype2 libtool version)

        FREETYPE_VERSION=`$FREETYPE_CONFIG --version`

        VERSION_DEC=`echo $FREETYPE_VERSION | awk -F. '{printf("%d\n", 10000*$1 + 100*$2 + $3)};'`
        MIN_VERSION_DEC=`echo $FREETYPE_MIN_VERSION | awk -F. '{printf("%d\n", 10000*$1 + 100*$2 + $3)};'`
        if test $VERSION_DEC -lt $MIN_VERSION_DEC; then
          AC_MSG_RESULT($FREETYPE_VERSION - Too old)
          use_ft="no ($FREETYPE_VERSION found; version $FREETYPE_MIN_VERSION from release $FREETYPE_MIN_RELEASE required)"
        else
          AC_MSG_RESULT($FREETYPE_VERSION - OK)

	  ft_NONPKGCONFIG_CFLAGS=`$FREETYPE_CONFIG --cflags`
	  ft_NONPKGCONFIG_LIBS=`$FREETYPE_CONFIG --libs`
        fi
      fi
    fi
  fi
  ft_CFLAGS="$FREETYPE_CFLAGS $FONTCONFIG_CFLAGS"
  ft_LIBS="$FREETYPE_LIBS $FONTCONFIG_LIBS"
])

if test "x$use_ft" = "xyes"; then
  temp_save_libs="$LIBS"
  temp_save_cflags="$CFLAGS"
  LIBS="$LIBS $ft_LIBS"
  CFLAGS="$CFLAGS $ft_CFLAGS"
  AC_CHECK_MEMBER(FT_Bitmap_Size.y_ppem,
                  HAVE_FT_BITMAP_SIZE_Y_PPEM=1,
                  HAVE_FT_BITMAP_SIZE_Y_PPEM=0,
                  [#include <ft2build.h>
                   #include FT_FREETYPE_H])
  AC_DEFINE_UNQUOTED(HAVE_FT_BITMAP_SIZE_Y_PPEM,$HAVE_FT_BITMAP_SIZE_Y_PPEM,
                     [FT_Bitmap_Size structure includes y_ppem field])

  AC_CHECK_FUNCS(FT_GlyphSlot_Embolden FT_Load_Sfnt_Table)

  LIBS="$temp_save_libs"
  CFLAGS="$temp_save_cflags"
fi

dnl ===========================================================================

AC_ARG_ENABLE(pthread,
  AS_HELP_STRING([--disable-pthread],
		 [Do not use pthread]),
  [use_pthread=$enableval], [use_pthread=auto])

have_pthread=no
if test "x$use_pthread" != "xno"; then
  AC_CHECK_HEADERS([pthread.h], have_pthread=yes, have_pthread=no)
fi
AM_CONDITIONAL(HAVE_PTHREAD, test "x$have_pthread" = "xyes")
if test "x$have_pthread" = xno -a "x$use_pthread" = xyes; then
  AC_MSG_ERROR([pthread requested but not found])
fi

dnl ===========================================================================

CAIRO_BACKEND_ENABLE(ps, PostScript, ps, PS_SURFACE, yes, [
    # The ps backend requires zlib.
    use_ps=$have_libz
    ps_NONPKGCONFIG_LIBS=-lz
])

test_ps=no
if test "x$use_ps" = "xyes"; then
  AC_CHECK_PROG(GS, gs, gs)
  if test "$GS"; then
    AC_DEFINE([CAIRO_CAN_TEST_PS_SURFACE], 1, [Define to 1 if the PS backend can be tested (needs ghostscript)])
    test_ps="yes"
  else
    AC_MSG_WARN([PS backend will not be tested since ghostscript is not available])
    test_ps="no (requires ghostscript)"
  fi
fi

AM_CONDITIONAL(CAIRO_CAN_TEST_PS_SURFACE, test "x$test_ps" = "xyes")

dnl ===========================================================================

CAIRO_BACKEND_ENABLE(pdf, PDF, pdf, PDF_SURFACE, yes, [
    # The pdf backend requires zlib.
    use_pdf=$have_libz
    pdf_NONPKGCONFIG_LIBS=-lz
])

POPPLER_VERSION_REQUIRED=0.8.0
test_pdf=no
if test "x$use_pdf" = "xyes"; then
  poppler_DEPENDENCY="poppler-glib >= $POPPLER_VERSION_REQUIRED"
  PKG_CHECK_MODULES(POPPLER, $poppler_DEPENDENCY pango gtk+-2.0,
		    [_CHECK_FUNCS_WITH_FLAGS(poppler_page_render_to_pixbuf, [$POPPLER_CFLAGS], [$POPPLER_LIBS],
                    [test_pdf=yes],
		    [AC_MSG_RESULT(no); test_pdf="no (requires $poppler_DEPENDENCY)"])],
		    [AC_MSG_RESULT(no); test_pdf="no (requires $poppler_DEPENDENCY)"])
  if test "x$test_pdf" = "xyes"; then
    AC_DEFINE([CAIRO_CAN_TEST_PDF_SURFACE], 1, [Define to 1 if the PDF backend can be tested (need poppler and other dependencies for pdf2png)])
  else
    AC_MSG_WARN([PDF backend will not be tested since poppler >= $POPPLER_VERSION_REQUIRED is not available])
  fi
fi

AM_CONDITIONAL(CAIRO_CAN_TEST_PDF_SURFACE, test "x$test_pdf" = "xyes")
AC_SUBST(POPPLER_CFLAGS)
AC_SUBST(POPPLER_LIBS)

AM_CONDITIONAL(CAIRO_HAS_MULTI_PAGE_SURFACES, test "x$use_ps" = "xyes" -o "x$use_pdf" = "xyes")

dnl ===========================================================================

CAIRO_BACKEND_ENABLE(svg, SVG, svg, SVG_SURFACE, yes, [
  if test "x$use_png" != "xyes"; then
    use_svg="no (requires --enable-png)"
  fi
])

LIBRSVG_VERSION_REQUIRED=2.15.0
test_svg=no
if test "x$use_svg" = "xyes"; then
  librsvg_DEPENDENCY="librsvg-2.0 >= $LIBRSVG_VERSION_REQUIRED"
  PKG_CHECK_MODULES(LIBRSVG, $librsvg_DEPENDENCY gdk-2.0,
		    [_CHECK_FUNCS_WITH_FLAGS(rsvg_pixbuf_from_file, [$LIBRSVG_CFLAGS], [$LIBRSVG_LIBS],
                    [test_svg=yes],
		    [AC_MSG_RESULT(no); test_svg="no (requires $librsvg_DEPENDENCY)"])],
		    [AC_MSG_RESULT(no); test_svg="no (requires $librsvg_DEPENDENCY)"])
  if test "x$test_svg" = "xyes"; then
    AC_DEFINE([CAIRO_CAN_TEST_SVG_SURFACE], 1, [Define to 1 if the SVG backend can be tested])
  else
    AC_MSG_WARN([SVG backend will not be tested since librsvg >= $LIBRSVG_VERSION_REQUIRED is not available])
  fi
fi

AM_CONDITIONAL(CAIRO_CAN_TEST_SVG_SURFACE, test "x$test_svg" = "xyes")
AC_SUBST(LIBRSVG_CFLAGS)
AC_SUBST(LIBRSVG_LIBS)

dnl ===========================================================================
dnl dump backend checking results
AC_CACHE_SAVE

dnl ===========================================================================
dnl Checks for precise integer types
AC_CHECK_HEADERS([stdint.h signal.h inttypes.h sys/int_types.h])
AC_CHECK_TYPES([uint64_t, uint128_t])

dnl ===========================================================================
dnl check for CPU affinity support
AC_CHECK_HEADERS([sched.h], [
  AC_CHECK_FUNCS([sched_getaffinity], [
    AC_DEFINE([HAVE_SCHED_GETAFFINITY], [1],
	      [Define to 1 if you have Linux compatible sched_getaffinity])
  ])
])

AC_CHECK_LIB(rt, sched_yield, [RT_LIBS=-lrt], [RT_LIBS=])
CAIROPERF_LIBS=$RT_LIBS

dnl ===========================================================================
dnl Checks for misc headers
AC_CHECK_HEADERS([libgen.h])

dnl ===========================================================================
dnl check compiler flags
AC_DEFUN([CAIRO_CC_TRY_FLAG], [
  AC_MSG_CHECKING([whether $CC supports $1])

  cairo_save_CFLAGS="$CFLAGS"
  CFLAGS="$CFLAGS $1"

  AC_COMPILE_IFELSE([ ], [cairo_cc_flag=yes], [cairo_cc_flag=no])
  CFLAGS="$cairo_save_CFLAGS"

  if test "x$cairo_cc_flag" = "xyes"; then
    ifelse([$2], , :, [$2])
  else
    ifelse([$3], , :, [$3])
  fi
  AC_MSG_RESULT([$cairo_cc_flag])
])


dnl Use lots of warning flags with with gcc and compatible compilers

dnl Note: if you change the following variable, the cache is automatically
dnl skipped and all flags rechecked.  So there's no need to do anything
dnl else.  If for any reason you need to force a recheck, just change
dnl MAYBE_WARN in an ignorable way (like adding whitespace)

MAYBE_WARN="-Wall -Wextra \
-Wsign-compare -Werror-implicit-function-declaration \
-Wpointer-arith -Wwrite-strings -Wstrict-prototypes \
-Wmissing-prototypes -Wmissing-declarations -Wnested-externs \
-Wpacked -Wswitch-enum -Wmissing-format-attribute \
-Wstrict-aliasing=2 -Winit-self -Wunsafe-loop-optimizations \
-Wdeclaration-after-statement -Wold-style-definition \
-Wno-missing-field-initializers -Wno-unused-parameter \
-Wno-attributes -Wno-long-long -Winline"


# invalidate cached value if MAYBE_WARN has changed
if test "x$cairo_cv_warn_maybe" != "x$MAYBE_WARN"; then
	unset cairo_cv_warn_cflags
fi
AC_CACHE_CHECK([for supported warning flags], cairo_cv_warn_cflags, [
	echo
	WARN_CFLAGS=""

	# Some warning options are not supported by all versions of
	# gcc, so test all desired options against the current
	# compiler.
	#
	# Note that there are some order dependencies
	# here. Specifically, an option that disables a warning will
	# have no net effect if a later option then enables that
	# warnings, (perhaps implicitly). So we put some grouped
	# options (-Wall and -Wextra) up front and the -Wno options
	# last.

	for W in $MAYBE_WARN; do
		CAIRO_CC_TRY_FLAG([$W], [WARN_CFLAGS="$WARN_CFLAGS $W"])
	done

	cairo_cv_warn_cflags=$WARN_CFLAGS
	cairo_cv_warn_maybe=$MAYBE_WARN

	AC_MSG_CHECKING([which warning flags were supported])])
WARN_CFLAGS="$cairo_cv_warn_cflags"
CAIRO_CFLAGS="$CAIRO_CFLAGS $WARN_CFLAGS"

# We only wish to enable attribute(warn_unused_result) if we can prevent
# gcc from generating thousands of warnings about the misapplication of the
# attribute to void functions and variables.
AC_MSG_CHECKING([how to enable unused result warnings])
warn_unused_result=""
if echo $WARN_CFLAGS | grep -e '-Wno-attributes' >/dev/null; then
    AC_TRY_COMPILE([__attribute__((__warn_unused_result__))
	int f (int i) { return i; }], [],
	[warn_unused_result="__attribute__((__warn_unused_result__))"])
fi
AC_DEFINE_UNQUOTED([WARN_UNUSED_RESULT], [$warn_unused_result],
	  [Define to the value your compiler uses to support the warn-unused-result attribute])
AC_MSG_RESULT([$warn_unused_result])

CAIRO_CC_TRY_FLAG([-fno-strict-aliasing],
		  [CAIRO_CFLAGS="$CAIRO_CFLAGS -fno-strict-aliasing"])

AC_SUBST(CAIRO_REQUIRES)
AC_SUBST(CAIRO_NONPKGCONFIG_CFLAGS)
AC_SUBST(CAIRO_NONPKGCONFIG_LIBS)
AC_SUBST(CAIRO_CFLAGS)
AC_SUBST(CAIRO_LDADD)
AC_SUBST(CAIRO_LIBS)
AC_SUBST(CAIROPERF_LIBS)

dnl ===========================================================================
dnl Check for gtk-doc and docbook

GTK_DOC_CHECK([1.6])

dnl ===========================================================================
dnl Check for recent pkg-config which supports Requires.private

case `$PKG_CONFIG --version` in
[0.?|0.?.?|0.1[0-7]|0.1[0-7].?]) PKGCONFIG_REQUIRES="Requires"; ;;
*) PKGCONFIG_REQUIRES="Requires.private"; ;;
esac

AC_SUBST(PKGCONFIG_REQUIRES)

dnl ===========================================================================
dnl Check for MMX

MMX_CFLAGS="-mmmx -Winline --param inline-unit-growth=10000 --param large-function-growth=10000"

have_mmx_intrinsics=no
AC_MSG_CHECKING(for MMX/SSE intrinsics in the compiler)
xserver_save_CFLAGS=$CFLAGS
CFLAGS="$CFLAGS $MMX_CFLAGS"
AC_COMPILE_IFELSE([
#if defined(__GNUC__) && (__GNUC__ < 3 || (__GNUC__ == 3 && __GNUC_MINOR__ < 4))
#error "Need GCC >= 3.4 for MMX intrinsics"
#endif
#include <mmintrin.h>
int main () {
    __m64 v = _mm_cvtsi32_si64 (1);
    __m64 w = _mm_slli_si64 (v, 1);
    v = _mm_or_si64 (v, w);
    return _mm_cvtsi64_si32 (v);
}], have_mmx_intrinsics=yes)
CFLAGS=$xserver_save_CFLAGS
AC_MSG_RESULT($have_mmx_intrinsics)

dnl ===========================================================================

cairo_has_lcov=no
AC_ARG_ENABLE(gcov,
  AS_HELP_STRING([--enable-gcov],
		 [Enable gcov]),
  [use_gcov=$enableval], [use_gcov=no])

if test "x$use_gcov" = "xyes"; then
  dnl we need gcc:
  if test "$GCC" != "yes"; then
    AC_MSG_ERROR([GCC is required for --enable-gcov])
  fi

  dnl Check if ccache is being used
  AC_CHECK_PROG(SHTOOL, shtool, shtool)
  case `$SHTOOL path $CC` in
    *ccache*[)] gcc_ccache=yes;;
    *[)] gcc_ccache=no;;
  esac

  if test "$gcc_ccache" = "yes" && (test -z "$CCACHE_DISABLE" || test "$CCACHE_DISABLE" != "1"); then
    AC_MSG_ERROR([ccache must be disabled when --enable-gcov option is used. You can disable ccache by setting environment variable CCACHE_DISABLE=1.])
  fi

  ltp_version_list="1.4 1.5"
  AC_CHECK_PROG(LTP, lcov, lcov)
  AC_CHECK_PROG(LTP_GENHTML, genhtml, genhtml)

  if test "$LTP"; then
    AC_CACHE_CHECK([for ltp version], cairo_cv_ltp_version, [
      cairo_cv_ltp_version=invalid
      ltp_version=`$LTP -v 2>/dev/null | $SED -e 's/^.* //'`
      for ltp_check_version in $ltp_version_list; do
        if test "$ltp_version" = "$ltp_check_version"; then
          cairo_cv_ltp_version="$ltp_check_version (ok)"
        fi
      done
    ])
  fi

  case $cairo_cv_ltp_version in
    ""|invalid[)]
      ;;
    *)
      cairo_has_lcov=yes
      ;;
  esac

  if test "x$cairo_has_lcov" != "xyes"; then
    AC_MSG_ERROR([[To enable code coverage reporting you must have one of the following LTP versions installed: $ltp_version_list.
Please install the Linux Test Project [http://ltp.sourceforge.net/], and try again.]])
   fi

  if test -z "$LTP_GENHTML"; then
    AC_MSG_ERROR([[Could not find genhtml from the LTP package.
Please install the Linux Test Project [http://ltp.sourceforge.net/], and try again.]])
  fi

  AC_DEFINE(HAVE_GCOV, 1, [Whether you have gcov])
dnl  PHP_ADD_MAKEFILE_FRAGMENT($abs_srcdir/Makefile.gcov, $abs_srcdir)

  dnl Remove all optimization flags from CFLAGS
  changequote({,})
  CFLAGS=`echo "$CFLAGS" | $SED -e 's/-O[0-9]*//g'`
  CAIRO_CFLAGS=`echo "$CAIRO_CFLAGS" | $SED -e 's/-O[0-9]*//g'`
  changequote([,])

  dnl Add the special gcc flags
  CAIRO_CFLAGS="$CAIRO_CFLAGS -O0 -fprofile-arcs -ftest-coverage"
  CAIRO_LDADD="$CAIRO_LDADD -lgcov"
fi
AM_CONDITIONAL(CAIRO_HAS_LCOV, test "x$cairo_has_lcov" = "xyes")

dnl ===========================================================================

AC_ARG_ENABLE(test-surfaces,
  AS_HELP_STRING([--enable-test-surfaces],
		 [Add backends for more test suite coverage (no additional public functionality)]),
  [use_test_surfaces=$enableval], [use_test_surfaces=no])

AM_CONDITIONAL(CAIRO_HAS_TEST_SURFACES, test "x$use_test_surfaces" = "xyes")
if test "x$use_test_surfaces" = "xyes"; then
  AC_DEFINE(CAIRO_HAS_TEST_SURFACES, 1, [define in the extra test surface have been built into cairo for the test suite])
fi

dnl ===========================================================================

AC_ARG_ENABLE(some-floating-point,
  AS_HELP_STRING([--disable-some-floating-point],
                 [Disable certain code paths that rely heavily on double precision
                  floating-point calculation. This option can improve
                  performance on systems without a double precision floating-point
                  unit, but might degrade performance on those that do.]), [
if test "x$enableval" = "xno"; then
  # A value of 'no' for $enableval means that they want to disable, which
  # means 'yes' for $disable_some_floating_point.
  disable_some_floating_point=yes
fi
], [disable_some_floating_point=no])

AM_CONDITIONAL(DISABLE_SOME_FLOATING_POINT,
               test "x$disable_some_floating_point" = "xyes")
if test "x$disable_some_floating_point" = "xyes"; then
  AC_DEFINE(DISABLE_SOME_FLOATING_POINT, 1,
            [Define to 1 to disable certain code paths that rely heavily on
             double precision floating-point calculation])
fi

dnl ===========================================================================

AC_CONFIG_COMMANDS([src/cairo-features.h],
[
	featuresfile=src/cairo-features.h
	outfile=$featuresfile.tmp
	AC_MSG_NOTICE([creating $featuresfile])
	cat > $outfile <<_EOF
/* Generated by configure.  Do not edit */
#ifndef CAIRO_FEATURES_H
#define CAIRO_FEATURES_H

#ifdef  __cplusplus
# define CAIRO_BEGIN_DECLS  extern "C" {
# define CAIRO_END_DECLS    }
#else
# define CAIRO_BEGIN_DECLS
# define CAIRO_END_DECLS
#endif

#ifndef cairo_public
# define cairo_public
#endif

#define CAIRO_VERSION_MAJOR $CAIRO_VERSION_MAJOR
#define CAIRO_VERSION_MINOR $CAIRO_VERSION_MINOR
#define CAIRO_VERSION_MICRO $CAIRO_VERSION_MICRO

#define CAIRO_VERSION_STRING "$CAIRO_VERSION_MAJOR.$CAIRO_VERSION_MINOR.$CAIRO_VERSION_MICRO"

_EOF
	for FEATURE in $CAIRO_FEATURES; do
		echo "#define CAIRO_HAS_$FEATURE 1" >> $outfile
	done
	echo '' >> $outfile
	for FEATURE in $CAIRO_NO_FEATURES; do
		echo "/*#undef CAIRO_HAS_$FEATURE */" >> $outfile
	done

	echo '' >> $outfile
	echo '#endif' >> $outfile

	if cmp -s $outfile $featuresfile; then
	  AC_MSG_NOTICE([$featuresfile is unchanged])
	  rm -f $outfile
	else
	  mv $outfile $featuresfile
	fi
],[
	CAIRO_FEATURES='$CAIRO_FEATURES'
	CAIRO_NO_FEATURES='$CAIRO_NO_FEATURES'
	CAIRO_VERSION_MAJOR=$CAIRO_VERSION_MAJOR
	CAIRO_VERSION_MINOR=$CAIRO_VERSION_MINOR
	CAIRO_VERSION_MICRO=$CAIRO_VERSION_MICRO
])
AC_CONFIG_COMMANDS([src/cairo-no-features.h],
[
	nofeaturesfile=src/cairo-no-features.h
	outfile=$nofeaturesfile.tmp
	AC_MSG_NOTICE([creating $nofeaturesfile])
	cat > $outfile <<_EOF
/* Generated by configure.  Do not edit */
#ifndef CAIRO_NO_FEATURES_H
#define CAIRO_NO_FEATURES_H

#include <cairo-features.h>

/* This is a dummy header, to trick gtk-doc only */

_EOF
	for FEATURE in $CAIRO_NO_FEATURES; do
		echo "#define CAIRO_HAS_$FEATURE 1" >> $outfile
	done

	echo '' >> $outfile
	echo '#endif' >> $outfile

	if cmp -s $outfile $nofeaturesfile; then
	  AC_MSG_NOTICE([$nofeaturesfile is unchanged])
	  rm -f $outfile
	else
	  mv $outfile $nofeaturesfile
	fi
],[
	CAIRO_NO_FEATURES='$CAIRO_NO_FEATURES'
])
AC_CONFIG_FILES([
Makefile
src/Makefile
src/cairo.pc
])

AC_OUTPUT

dnl ===========================================================================

V="$CAIRO_VERSION_MAJOR.$CAIRO_VERSION_MINOR.$CAIRO_VERSION_MICRO"
echo ""
echo "cairo (version $V) will be compiled with the following surface backends:"
echo "  image:         yes (always builtin)"
echo "  Xlib:          $use_xlib"
echo "  Xlib Xrender:  $use_xlib_xrender"
echo "  Quartz:        $use_quartz"
echo "  Quartz-image:  $use_quartz_image"
echo "  XCB:           $use_xcb"
echo "  Win32:         $use_win32"
echo "  OS2:           $use_os2"
echo "  PostScript:    $use_ps"
echo "  PDF:           $use_pdf"
echo "  SVG:           $use_svg"
echo "  glitz:         $use_glitz"
echo "  BeOS:          $use_beos"
echo "  DirectFB:      $use_directfb"
echo ""
echo "the following font backends:"
echo "  FreeType:      $use_ft"
echo "  Win32:         $use_win32_font"
echo "  Quartz:        $use_quartz_font"
echo ""
echo "the following features:"
echo "  PNG functions: $use_png"
echo ""
echo "and the following debug options:"
echo "  gcov support:  $use_gcov"
echo "  test surfaces: $use_test_surfaces"
echo "  ps testing:    $test_ps"
echo "  pdf testing:   $test_pdf"
echo "  svg testing:   $test_svg"
if test x"$use_win32" = "xyes"; then
	echo "  win32 printing testing:    $test_win32_printing"
fi
echo ""
echo "using CFLAGS:"
echo $CAIRO_CFLAGS
echo ""

if test x"$use_ft"          != "xyes" && \
   test x"$use_win32_font"  != "xyes" && \
   test x"$use_quartz_font" != "xyes" ; then

   AC_MSG_ERROR([Cairo requires at least one font backend.
		  Please install freetype and fontconfig, then try again:
		  http://freetype.org/	http://fontconfig.org/
		])
fi

WARNING_MESSAGE='
*** The @BACKEND@ backend is still under active development and
*** is included in this release only as a preview. It does not
*** fully work yet and incompatible changes may yet be made
*** to @BACKEND@-backend specific API.
'

if test x"$use_glitz" = "xyes" ; then
   echo "$WARNING_MESSAGE" | sed 's/@BACKEND@/Glitz/'
fi

if test x"$use_xcb" = "xyes" ; then
   echo "$WARNING_MESSAGE" | sed 's/@BACKEND@/XCB/'
fi

if test x"$use_quartz_image" = "xyes" ; then
   echo "$WARNING_MESSAGE" | sed 's/@BACKEND@/Quartz Image/'
fi

if test x"$use_os2" = "xyes" ; then
   echo "$WARNING_MESSAGE" | sed 's,@BACKEND@,OS/2,'
fi

if test x"$use_beos" = "xyes" ; then
   echo "$WARNING_MESSAGE" | sed 's/@BACKEND@/BeOS/'
fi

if test x"$use_directfb" = "xyes" ; then
     echo "$WARNING_MESSAGE" | sed 's/@BACKEND@/DirectFB/'
fi
